plugins {
    id 'java'
    id 'application'
    id 'org.beryx.jlink' version '2.24.0'
}

group('io.github.lucasstarsz')
version('0.0.1')
description('A model creation and editing tool for FastJ.')
application.getMainClass().set('tech.fastj.modeleditor.Main')
application.mainModule.set('fastj.modeleditor')


repositories.maven {
    url('https://jitpack.io/')
}
repositories.mavenCentral()
dependencies.implementation('com.github.fastjengine:FastJ:1.6.0-SNAPSHOT')


jlink {

    options.addAll(
            '--strip-debug',
            '--compress', '2',
            '--no-header-files',
            '--no-man-pages'
    )

    launcher {
        name = project.name
        noConsole = true
    }

    jpackage {

        def iconPath = 'project-resources/fastj_icon'
        def currentOs = org.gradle.internal.os.OperatingSystem.current()

        installerOptions += [
                '--description', project.description,
                '--vendor', project.group,
                '--app-version', project.version
        ]


        if (currentOs.windows) {
            installerType = 'msi'
            icon = "${iconPath}.ico"
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-shortcut']
        } else if (currentOs.linux) {
            installerType = 'deb'
            icon = "${iconPath}.png"
            installerOptions += ['--linux-shortcut']
        } else if (currentOs.macOsX) {
            installerType = 'pkg'
            icon = "${iconPath}.icns"
            installerOptions += ['--mac-package-name', project.name]
        }
    }
}

